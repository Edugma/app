apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: "androidx.navigation.safeargs.kotlin"

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    signingConfigs {
        tipapro {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 30
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.mospolytech.mospolyhelper"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 4
        versionName "0.3.0"


        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.tipapro
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->

            variant.buildConfigField "String", "URL_SCHEDULES_ALL", "\""+getProp('URL_SCHEDULES_ALL')+"\""
            variant.buildConfigField "String", "URL_SCHEDULES_SESSION_ALL", "\""+getProp('URL_SCHEDULES_SESSION_ALL')+"\""
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        // Sets Java compatibility to Java 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding = true
        viewBinding true
    }
}

static def getProp(String key){
    Properties props = new Properties()
    props.load(new FileInputStream(new File('secrets.properties')))
    if (props.containsKey(key)) {
        return props[key]
    } else {
        return ""
    }
}

dependencies {
    def nav_version = '2.3.4'
    def koin_version= "2.2.0-beta-1"
    def ktor_version= "1.5.2"
    def room_version = '2.2.6'
    def logback_version = "1.2.3"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.fragment:fragment-ktx:1.3.1"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'
    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'org.robolectric:robolectric:4.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


    // ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.4.4'


    // Koin for Kotlin
    implementation "org.koin:koin-core:$koin_version"
    // Koin Extended & experimental features
    implementation "org.koin:koin-core-ext:$koin_version"
    // Koin for Unit tests
    //testImplementation "org.koin:koin-test:$koin_version"
    // Koin for Android
    implementation "org.koin:koin-android:$koin_version"
    // Koin AndroidX Scope feature
    implementation "org.koin:koin-androidx-scope:$koin_version"
    // Koin AndroidX ViewModel feature
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    // Koin AndroidX Fragment Factory
    implementation "org.koin:koin-androidx-fragment:$koin_version"
    // Koin AndroidX Experimental features
    implementation "org.koin:koin-androidx-ext:$koin_version"
    // Koin for Ktor Kotlin
    implementation "org.koin:koin-ktor:$koin_version"


    // Network
    implementation "io.ktor:ktor-client-android:$ktor_version"
    implementation "io.ktor:ktor-client-serialization:$ktor_version"
    implementation "io.ktor:ktor-client-logging:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"


    // Json
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'org.jsoup:jsoup:1.13.1'

    implementation 'com.budiyev.android:circular-progress-bar:1.2.2'

    implementation "androidx.paging:paging-runtime-ktx:3.0.0-beta01"

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    implementation ('com.github.bumptech.glide:recyclerview-integration:4.12.0') {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }
    implementation 'com.auth0.android:jwtdecode:2.0.0'
    implementation 'com.google.android:flexbox:2.0.1'
}
